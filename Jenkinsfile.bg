/**
 * Author: Leandro Beretta <lberetta@redhat.com>
 */

try {
    timeout(time: 10, unit: 'MINUTES') {

        node() {

            stage("Select Version") {
                env.version = selectVersion("app", "DevSnapshot", "dev");
            }

            stage("Deploy TEST") {
                def newTag = "TestCandidate-${version}"

                openshiftTag alias: "false",
                        destStream: "app",
                        destTag: newTag,
                        destinationNamespace: "test",
                        namespace: "dev",
                        srcStream: "app",
                        srcTag: "DevSnapshot-${version}",
                        verbose: "true"

                sh "oc project test"
                sh "oc patch dc app --patch '{\"spec\": { \"triggers\": [ { \"type\": \"ImageChange\", \"imageChangeParams\": { \"containerNames\": [ \"app\" ], \"from\": { \"kind\": \"ImageStreamTag\", \"namespace\": \"test\", \"name\": \"app:TestCandidate-$version\"}}}]}}' -n test"

                openshiftDeploy depCfg: "app",
                        namespace: "test"
            }

            stage("Integration Test") {
                // Complete this section with your actual integration tests

                def newTag = "ProdReady-${version}"

                openshiftTag alias: "false", destStream: "app", destTag: newTag, destinationNamespace: "prod", namespace: "test", srcStream: "app", srcTag: "TestCandidate-${version}", verbose: "false"
            }

            // Blue/Green

            def dest = "app-green"
            def active = ""

            stage("Deploy PRE-PROD") {
                sh "oc project prod"
                sh "oc get route blue-green -n prod -o jsonpath='{ .spec.to.name }' > active"

                active = readFile("active").trim()

                if (active == "app-green")
                    dest = "app-blue"

                sh "oc patch dc ${dest} --patch '{\"spec\": { \"triggers\": [ { \"type\": \"ImageChange\", \"imageChangeParams\": { \"containerNames\": [ \"$dest\" ], \"from\": { \"kind\": \"ImageStreamTag\", \"namespace\": \"prod\", \"name\": \"app:ProdReady-$version\"}}}]}}' -n prod"

                openshiftDeploy depCfg: dest,
                        namespace: 'prod'
            }

            stage("Switch Version") {
                input "Switch to new version?"
            }

            sh "oc patch route blue-green -n prod -p '{ \"spec\" : { \"to\" : { \"name\" : \"$dest\" } } }'"

            stage("Rollback PROD") {
                rollback = input message: "Rollback to previous version?",
                        parameters: [choice(choices: "Yes\nNo", description: '', name: 'Rollback')]
            }

            if (rollback.equals('Yes')) {
                sh "oc patch route blue-green -n prod -p '{ \"spec\" : { \"to\" : {\"name\" : \"$active\" } } }'"
                echo "Deployment rolled back successfully"
            } else {
                echo "Deployment finished successfully"
            }
        }
    }
} catch (err) {
    echo "Caught: ${err}"
    currentBuild.result = 'FAILURE'

    throw err
}

def getVersionFromPom(pom) {
    def matcher = readFile(pom) =~ '<version>(.+)</version>'
    matcher ? matcher[0][1] : null
}

def selectVersion(imageName, tagPrefix, namespace) {
    def selectedTag = ""

    sh "oc get istag -n ${namespace} --sort-by='{.metadata.name}'| grep ${imageName}| grep ${tagPrefix} | awk '{print \$1}' | awk -F ':' '{print \$2}' | awk -F '-' '{print \$2}' | tail -5 > tags"

    def tags = readFile("tags").trim()

    selectedTag = input message: "Choose version",
            parameters: [choice(choices: tags, description: '', name: 'Versions')]

    return selectedTag
}
